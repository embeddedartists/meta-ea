#@TYPE: Machine
#@NAME: Embedded Artists i.MX6 Quad COM Board
#@SOC: i.MX6Q
#@DESCRIPTION: Machine configuration for Embedded Artists i.MX6 Quad COM Board
#@MAINTAINER: EA <imx@EmbeddedArtists.com>

require conf/machine/include/imx-base.inc
include conf/machine/include/arm/armv7a/tune-cortexa9.inc

SERIAL_CONSOLES = "115200;ttymxc3"

MACHINE_FEATURES += " pci wifi bluetooth nxp8987 mrvl8997"

MACHINEOVERRIDES =. "mx6:mx6q:"

PREFERRED_PROVIDER_virtual/kernel:imx-nxp-bsp = "linux-ea"
PREFERRED_PROVIDER_virtual/kernel = "linux-ea"
PREFERRED_RPROVIDER_kernel-module-imx-gpu-viv = "linux-ea"

PREFERRED_PROVIDER_linux-mfgtool = "linux-ea-mfgtool"

KERNEL_DEVICETREE = "imx6qea-com-kit_v2.dtb \
  imx6qea-com-kit_v2-ov5640.dtb \
  imx6qea-com-kit_v2-ov5640-pl.dtb \
  imx6qea-com-kit_v2-pcie.dtb \
  imx6qea-com-ptp.dtb \
"

PREFERRED_PROVIDER_u-boot = "u-boot-ea"
PREFERRED_PROVIDER_virtual/bootloader:imx-nxp-bsp = "u-boot-ea"

PREFERRED_PROVIDER_u-boot-mfgtool = "u-boot-ea-mfgtool"
PREFERRED_PROVIDER_u-boot-fw-utils = "u-boot-ea-fw-utils"

UBOOT_CONFIG ??= "sd"
UBOOT_CONFIG[sd] = "mx6qea-com_config"
UBOOT_CONFIG[mfgtool] = "mx6qea-com-ptp_config"

UBOOT_MAKE_TARGET = "${@bb.utils.contains("UBOOT_CONFIG", "mfgtool", "u-boot.imx", "all",d)}"
UBOOT_SUFFIX = "${@bb.utils.contains("UBOOT_CONFIG", "mfgtool", "imx", "img",d)}"
SPL_BINARY = "${@bb.utils.contains("UBOOT_CONFIG", "mfgtool", "", "SPL",d)}"

#
# Got the following build error: "make_dtb_boot_files No such file or directory"
# Seemed to be related to imx-base.inc where make_dtb_boot_files is called from
# IMAGE_BOOT_FILES. Setting IMAGE_BOOT_FILES like below solved the problem, but
# it needs further investigation.
#
IMAGE_BOOT_FILES = " \
    ${KERNEL_IMAGETYPE} \
    ${KERNEL_DEVICETREE} \
"

# wic support
IMAGE_BOOT_FILES:append = " \
        boot.scr \
"
WKS_FILE_DEPENDS += " \
        u-boot-script-ea \
        e2fsprogs-native \
        bmap-tools-native \
"
WKS_FILE = "imx-uboot-spl-bootpart.wks.in"
