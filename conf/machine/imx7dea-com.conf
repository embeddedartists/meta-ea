#@TYPE: Machine
#@NAME: Embedded Artists i.MX7 Dual COM Board
#@SOC: i.MX7D
#@DESCRIPTION: Machine configuration for Embedded Artists i.MX7 Dual COM Board
#@MAINTAINER: EA <imx@EmbeddedArtists.com>

include conf/machine/include/imx-base.inc
include conf/machine/include/tune-cortexa7.inc

SERIAL_CONSOLES = "115200;ttymxc0"

MACHINE_FEATURES += " pci wifi bluetooth nxp8987 mrvl8997"

MACHINEOVERRIDES =. "mx7:mx7d:"

PREFERRED_PROVIDER_virtual/kernel_imx = "linux-ea"
PREFERRED_PROVIDER_virtual/kernel = "linux-ea"

PREFERRED_PROVIDER_linux-mfgtool = "linux-ea-mfgtool"

KERNEL_DEVICETREE = "imx7dea-com-kit_v2.dtb \
  imx7dea-com-kit_v2-m4.dtb \
  imx7dea-com-kit_v2-ov5640.dtb \
  imx7dea-com-kit_v2-pcie.dtb \
  imx7dea-com-ptp.dtb \
"

PREFERRED_PROVIDER_u-boot = "u-boot-ea"
PREFERRED_PROVIDER_u-boot_mx7 = "u-boot-ea"
PREFERRED_PROVIDER_virtual/bootloader_imx = "u-boot-ea"

PREFERRED_PROVIDER_u-boot-mfgtool = "u-boot-ea-mfgtool"
PREFERRED_PROVIDER_u-boot-fw-utils = "u-boot-ea-fw-utils"

UBOOT_CONFIG ??= "sd"
UBOOT_CONFIG[sd] = "mx7dea-com_config"
UBOOT_CONFIG[mfgtool] = "mx7dea-com_config"

XSERVER += "mesa-driver-swrast"

UBOOT_MAKE_TARGET = "${@bb.utils.contains("UBOOT_CONFIG", "mfgtool", "u-boot.imx", "all",d)}"
UBOOT_SUFFIX = "${@bb.utils.contains("UBOOT_CONFIG", "mfgtool", "imx", "img",d)}"
SPL_BINARY = "${@bb.utils.contains("UBOOT_CONFIG", "mfgtool", "", "SPL",d)}"

#
# Got the following build error: "make_dtb_boot_files No such file or directory"
# Seemed to be related to imx-base.inc where make_dtb_boot_files is called from
# IMAGE_BOOT_FILES. Setting IMAGE_BOOT_FILES like below solved the problem, but
# it needs further investigation.
#
IMAGE_BOOT_FILES = " \
    ${KERNEL_IMAGETYPE} \
    ${KERNEL_DEVICETREE} \
"

# wic support
IMAGE_BOOT_FILES_append = " \
        boot.scr \
"
WKS_FILE_DEPENDS += " \
        u-boot-script-ea \
        e2fsprogs-native \
        bmap-tools-native \
"
WKS_FILE = "imx-uboot-spl-bootpart.wks.in"
